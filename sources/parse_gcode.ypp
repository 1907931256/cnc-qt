/* -*-c++-*- */

%{
/****************************************************************************
 * C# to Qt portation, Linux developing                                     *
 * flex/bison G-Code parser                                                 *
 * Copyright (C) 2015-2018 by Eduard Kalinowski                             *
 * Germany, Lower Saxony, Hanover                                           *
 * eduard_kalinowski@yahoo.de                                               *
 *                                                                          *
 * C# project CNC-controller-for-mk1                                        *
 * https://github.com/selenur/CNC-controller-for-mk1                        *
 *                                                                          *
 * The Qt project                                                           *
 * https://github.com/eduard-x/cnc-qt                                       *
 *                                                                          *
 * CNC-Qt is free software; may be distributed and/or modified under the    *
 * terms of the GNU General Public License version 3 as published by the    *
 * Free Software Foundation and appearing in the file LICENSE_GPLv3         *
 * included in the packaging of this file.                                  *
 *                                                                          *
 * This program is distributed in the hope that it will be useful,          *
 * but WITHOUT ANY WARRANTY; without even the implied warranty of           *
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            *
 * GNU General Public License for more details.                             *
 *                                                                          *
 * You should have received a copy of the GNU Lesser General Public         *
 * License along with CNC-Qt. If not, see  http://www.gnu.org/licenses      *
 ****************************************************************************/

#if HAVE_CONFIG_H
# include <config.h>
#endif

#include <stdio.h>
#include <stdlib.h>
#include <string.h>
#include <ctype.h>

#define YYERROR_VERBOSE 42
#define YYDEBUG 1
#define YYMAXDEPTH 1000000

#define __NOEXTENSIONS__ 1


#include <QString>
#include <QChar>
#include <QDebug>
#include <QVector>


#include "Settings.h"


#include "GCode.h"

extern int gcode_lineno;
static int gcodecharno = 0;

extern int gcode_parse (void);
extern void gcode_error (const char *c);
extern int gcode_lex (void);
extern int gcode_lex_destroy (void);


bool set_data_float(char c, float f);

// extern struct addParam;

// initializing for vector
ParserData *gcode_data = new ParserData();

%}

%name-prefix "gcode_"


%token NEW_LINE
%token IF
%token ENDIF
%token WHILE
%token ENDWHILE
%token GOTO
%token POW
%token SIN
%token COS
%token SQRT

%token InvalidCharacter


%token Eol


%union {
    int       i_val;
    float     f_val;
    char      c_val;
    char*     s_val;
    class DataOperation* op;
    class ParserData* g;
}


%token <f_val> FVAL
%token <s_val> SVAL COMMENTAR TERM VARIABLE
%token <i_val> IVAL

%token <c_val> CMD_CHAR PARAM_CHAR LABEL

%type <op>     EXPR

%token SIN_FUNC COS_FUNC POW_FUNC SQRT_FUNC
%token CMP_EQ CMP_LE CMP_GT CMP_LT CMP_GE CMP_NE

%right '='
%left  '-' '+'
%left  '*' '/'
%right '[' 
%left  ']'
%left  UMINUS


%right ELSE 

%start parse_gcode

%%

parse_gcode: 
        | glines
        ;


glines: gline 
        | glines gline 
        ;


gline:  CMD_CHAR IVAL
        {
            // the new line, create new object
            // for cases, when line begins with g, m command or n: label
            // has already command data
            if (gcode_data->gCmd >= 0) {
                gcode_data = new ParserData(gcode_data);
            }
            gcode_data->numberLine = gcode_lineno;

            switch ($1 | 0x20){
                case 'g' :
                    gcode_data->gCmd = (short)$2;
                    // add only commands to the list
                    // which will be interpretated
                    switch ($2){ // commands are known 
                        case 0:  // rapid moving
                        case 1:  // linear moving
                        case 2:  // clockwise arc
                        case 3:  // counter-clockwise arc
                        case 4:
                        case 17: // XY plane
                        case 18: // YZ plane
                        case 19: // ZX plane
                        case 20: // units: inches
                        case 21: // units: millimeters
                        case 28: // go to home pos
                        case 40: // compensation off
                        case 90: // set to absolute positioning 
                        case 91: // set to relative positioning
                        case 92:
                        case 161:
                        case 162:
                            break;
                       
                        default:
                            qWarning() << "G-Code is not decoded:" << $2;
                            gcode_data->decoded = false;
                            break;
                    }
                    break;

                case 'm' :
                    // has already M command
                    if (gcode_data->mCmd >= 0) {
                        gcode_data = new ParserData(gcode_data);
                    }
                    gcode_data->mCmd = (short)$2;
                    // TODO only for known codes
                    break;
 
                default:
                    // not decoded, it's possible other code? 
                    // TODO: not critical message 
                    break;
            }
        }
        | CMD_CHAR FVAL
        {
            // TODO extended, with point G or M command
            qWarning() << $1 << "-Code is not decoded:" << $2;
        }
        | LABEL IVAL 
        {
             gcode_data->labelNum = $2;
        }
        | params
        | vardefs
        | COMMENTAR 
        {
            if (strlen($1)) {
                gcode_data->lineComment = QString($1).simplified();
                gcode_data->numberLine = gcode_lineno;
                free($1);
            }
        }
        | NEW_LINE
        {
            GData::dataVector << *gcode_data;
            gcode_data = new ParserData(gcode_data);
            gcode_data->numberLine = (gcode_lineno+1); // for init
        }
        ;

vardefs: vardef 
        | vardefs vardef
        ;


vardef: VARIABLE '=' EXPR
        {
            // TODO: check, variable exists?
            if ($3->varType == DATA::FVAL) {
                GData::dataVaris.insert($1, $3->result);
//                 float f = GData::dataVaris[$3->vName];
//                 GData::dataVaris[$3->vName] = f;
//                 qInfo() << "new definition" << $1;
            }
            if ($3->varType == DATA::SVAL) { // TODO
//                 GData::dataVaris.insert($3->vName, $3->result);
//                 qInfo() << $3->result;
            }
        }
        ;


params: param
        | params param
        ;


param:  PARAM_CHAR EXPR
        {
            // when line begins with coordinates 
            if (set_data_float(($1 | 0x20), $2->result) == false) {
                gcode_data->decoded = false;
                printf("not decoded char %c, line %d\n", $1, gcode_lineno);
            }
        
            if ($2) {
                gcode_data->opVector << $2;
            }
            
            gcode_data->numberLine = gcode_lineno;
        }
        | PARAM_CHAR
        {
            // parameters without values
            if (gcode_data) {
                gcode_data->vParams << addParam { $1, false, 0 };
            }
        }
        ;


EXPR:   '[' EXPR ']' 
        {
            // root expression 
            $$ = $2;
        }
        | '[' EXPR '*' EXPR ']'
        {
            $$ = new DataOperation(EXP::MUL, DATA::FVAL);
            $$->expr1 = $2;
            $$->expr2 = $4;
            $$->result = $2->result * $4->result;
        }
        | '[' EXPR '/' EXPR ']'
        {
            $$ = new DataOperation(EXP::DIV, DATA::FVAL);
            $$->expr1 = $2;
            $$->expr2 = $4;
            $$->result = $2->result / $4->result;
        }
        | '[' EXPR '+' EXPR ']'
        {
            $$ = new DataOperation(EXP::PLUS, DATA::FVAL);
            $$->expr1 = $2;
            $$->expr2 = $4;
            $$->result = $2->result + $4->result;
        }
        | '[' EXPR '-' EXPR ']'
        {
            $$ = new DataOperation(EXP::MINUS, DATA::FVAL);
            $$->expr1 = $2;
            $$->expr2 = $4;
            $$->result = $2->result - $4->result;
        }
        | '-' EXPR %prec UMINUS 
        {
            $$ = new DataOperation(EXP::NEG, DATA::FVAL);
            $$->expr1 = $2; 
            $$->result = (-$2->result);
        }
        | SIN_FUNC '[' EXPR ']'
        {
            $$ = new DataOperation(EXP::SIN, DATA::FVAL);
            $$->expr1 = $3; 
            $$->result = qSin($3->result);
        }
        | COS_FUNC '[' EXPR ']'
        {
            $$ = new DataOperation(EXP::COS, DATA::FVAL);
            $$->expr1 = $3; 
            $$->result = qCos($3->result);
        }
        | SQRT_FUNC '[' EXPR ']'
        {
            $$ = new DataOperation(EXP::SQRT, DATA::FVAL);
            $$->expr1 = $3; 
            $$->result = qSqrt($3->result);
        }
        | '[' EXPR POW_FUNC EXPR ']'
        {
            $$ = new DataOperation(EXP::POW, DATA::FVAL);
            $$->expr1 = $2;
            $$->expr2 = $4;
            $$->result = qPow($2->result, $4->result);
        }
        | IVAL 
        {
            $$ = new DataOperation(EXP::NONE, DATA::FVAL);
            $$->result = (float)$1;
        }
        | FVAL 
        {
            $$ = new DataOperation(EXP::NONE, DATA::FVAL);
            $$->result = $1;
        }
        | VARIABLE 
        {
            $$ = new DataOperation(EXP::NONE, DATA::SVAL);
            
            if (GData::dataVaris.contains($1)){
                $$->vName = $1;
                $$->result = GData::dataVaris[$1];
            }
            else {
                qInfo() << $1 << "was not defined";
            }
        }
        ;
%%


bool set_data_float(char c, float f) {
    switch (c){
        case 'x':
            gcode_data->coord.setX(f);
            break;
            
        case 'y':
            gcode_data->coord.setY(f);
            break;
            
        case 'z':
            gcode_data->coord.setZ(f);
            break;
            
        case 'i':
            gcode_data->useExtCoord = IJK; // TODO check conflicts
            gcode_data->extCoord.setX(f);
            break;
            
        case 'j':
            gcode_data->useExtCoord = IJK; // TODO check conflicts
            gcode_data->extCoord.setY(f);
            break;
            
        case 'k':
            gcode_data->useExtCoord = IJK; // TODO check conflicts
            gcode_data->extCoord.setZ(f);
            break;
            
        case 'a':
            gcode_data->useExtCoord = ABC; // TODO check conflicts
            gcode_data->extCoord.setX(f);
            break;
            
        case 'b':
            gcode_data->useExtCoord = ABC; // TODO check conflicts
            gcode_data->extCoord.setY(f);
            break;
            
        case 'c':
            gcode_data->useExtCoord = ABC; // TODO check conflicts
            gcode_data->extCoord.setZ(f);
            break;
            
        case 'u':
            gcode_data->useExtCoord = UVW; // TODO check conflicts
            gcode_data->extCoord.setX(f);
            break;
            
        case 'v':
            gcode_data->useExtCoord = UVW; // TODO check conflicts
            gcode_data->extCoord.setY(f);
            break;
            
        case 'w':
            gcode_data->useExtCoord = UVW; // TODO check conflicts
            gcode_data->extCoord.setZ(f);
            break;
            
        case 'r': // radius
        case 's': // pause sec
        case 'p': // pause msec
        case 'f': // feed
        case 'd': // diameter
        case 't': {// change tool
            gcode_data->vParams << addParam {c, true, f};
            break;
        }
            
        default:
            gcode_data->decoded = false;
            printf("not decoded char %c, line %d\n", c, gcode_lineno);
            return false;
            break;
    }
    return true;
}


void gcode_error (const char * error) {
  fprintf (stderr, "line %d: %s\n", gcode_lineno, error);
//   return 0;
}
