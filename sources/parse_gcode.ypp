/****************************************************************************
 * C# to Qt portation, Linux developing                                     *
 * flex/bison G-Code parser                                                 *
 * Copyright (C) 2015-2018 by Eduard Kalinowski                             *
 * Germany, Lower Saxony, Hanover                                           *
 * eduard_kalinowski@yahoo.de                                               *
 *                                                                          *
 * C# project CNC-controller-for-mk1                                        *
 * https://github.com/selenur/CNC-controller-for-mk1                        *
 *                                                                          *
 * The Qt project                                                           *
 * https://github.com/eduard-x/cnc-qt                                       *
 *                                                                          *
 * CNC-Qt is free software; may be distributed and/or modified under the    *
 * terms of the GNU General Public License version 3 as published by the    *
 * Free Software Foundation and appearing in the file LICENSE_GPLv3         *
 * included in the packaging of this file.                                  *
 *                                                                          *
 * This program is distributed in the hope that it will be useful,          *
 * but WITHOUT ANY WARRANTY; without even the implied warranty of           *
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            *
 * GNU General Public License for more details.                             *
 *                                                                          *
 * You should have received a copy of the GNU Lesser General Public         *
 * License along with CNC-Qt. If not, see  http://www.gnu.org/licenses      *
 ****************************************************************************/

%{


#if HAVE_CONFIG_H
# include <config.h>
#endif

#include <stdio.h>
#include <stdlib.h>
#include <string.h>
#include <ctype.h>

#define YYERROR_VERBOSE 42
#define YYDEBUG 1
#define YYMAXDEPTH 1000000

#define __NOEXTENSIONS__ 1


#include <QString>
#include <QChar>
#include <QDebug>
#include <QVector>


#include "Settings.h"


#include "GCodeScanner.h"

extern int gcode_lineno;

extern int gcode_parse (void);
extern void gcode_error (const char *c);
extern int gcode_lex (void);
extern int gcode_lex_destroy (void);


bool set_data_float(char c, float f);

// initializing for vector
GData *gcode_data = new GData();

short lastGCode = -1;

%}

%name-prefix "gcode_"


%token NEW_LINE
%token IF
%token ENDIF
%token WHILE
%token ENDWHILE
%token GOTO
%token POW
%token SIN
%token COS
%token SQRT

%token InvalidCharacter


%token Eol


%union {
    int       i_val;
    float     f_val;
    char      c_val;
    char*     s_val;
    class DataOperation* op;
    class ParserData* g;
}


%token <f_val> FVAL
%token <s_val> SVAL COMMENTAR TERM VARIABLE
%token <i_val> IVAL
%token <i_val> G_CMD M_CMD

%token <c_val> PARAM_CHAR LABEL

%type <op>     EXPR

%token SIN_FUNC COS_FUNC POW_FUNC SQRT_FUNC
%token CMP_EQ CMP_LE CMP_GT CMP_LT CMP_GE CMP_NE

%right '='
%left  '-' '+'
%left  '*' '/'
%right '[' 
%left  ']'
%left  UMINUS


%right ELSE 

%start parse_gcode

%%

parse_gcode: 
        | glines
        ;


glines: gline 
        | gline glines
        ;


gline:  G_CMD
        {
            // the new line, create new object
            // for cases, when line begins with g, m command or n: label
            // has already command data
            if (gcode_data->gCmd >= 0) {
                gcode_data = new GData(gcode_data);
            }
            gcode_data->numberLine = gcode_lineno;

            gcode_data->gCmd = (short)$1;
            
            // add only commands to the list
            // which will be interpretated
            switch ($1){ // commands are known 
                case 0:  // rapid moving
                case 1:  // linear moving
                case 2:  // clockwise arc
                case 3:  // counter-clockwise arc
                case 4:
                case 17: // XY plane
                case 18: // YZ plane
                case 19: // ZX plane
                case 20: // units: inches
                case 21: // units: millimeters
                case 28: // go to home pos
                case 40: // compensation off
                case 90: // set to absolute positioning 
                case 91: // set to relative positioning
                case 92:
                case 161:
                case 162:
                    lastGCode = (short)$1;
                    break;
                
                default:
                    qWarning() << "G-Code is not decoded:" << $1;
                    gcode_data->decoded = false;
                    break;
            }
        }
        | M_CMD
        {
            // has already M command
            if (gcode_data->mCmd >= 0) {
                gcode_data = new GData(gcode_data);
            }
            gcode_data->numberLine = gcode_lineno;
            gcode_data->mCmd = (short)$1;
        }
        | LABEL IVAL 
        {
             gcode_data->labelNum = $2;
        }
        | params
        | vardefs
        | COMMENTAR 
        {
            if (strlen($1)) {
                gcode_data->lineComment = QString($1).simplified();
                gcode_data->numberLine = gcode_lineno;
                free($1);
            }
        }
        | NEW_LINE
        {
            GCodeParser::dataVector << *gcode_data;
            gcode_data = new GData(gcode_data);
            gcode_data->numberLine = (gcode_lineno+1); // for init
        }
        ;

vardefs: vardef 
        | vardef vardefs
        ;


vardef: VARIABLE '=' EXPR
        {
            // TODO: check, variable exists?
            if ($3->varType == DATA::FVAL) {
                GCodeParser::dataVaris.insert($1, $3->result);
            }
            if ($3->varType == DATA::SVAL) { 
                // TODO
            }
        }
        ;


params: param
        | param params
        ;


param:  PARAM_CHAR EXPR
        {
            // when line begins with coordinates 
            if (set_data_float(($1 | 0x20), $2->result) == false) {
                gcode_data->decoded = false;
                printf("not decoded char %c, line %d\n", $1, gcode_lineno);
            }
        
            if ($2) {
                gcode_data->opVector << $2;
            }
            if (gcode_data->gCmd == -1) { // in case, corrdinates without g code
                if (lastGCode == 0 || lastGCode == 1) {
                    gcode_data->gCmd = lastGCode;
                }
            }
            
            gcode_data->numberLine = gcode_lineno;
        }
        | PARAM_CHAR
        {
            // parameters without values
            if (gcode_data) {
                gcode_data->vParams << addParam { $1, false, 0 };
            }
        }
        ;


EXPR:   '[' EXPR ']' 
        {
            // root expression 
            $$ = $2;
        }
        | EXPR '*' EXPR
        {
            $$ = new DataOperation(EXP::MUL, DATA::FVAL);
            $$->expr1 = $1;
            $$->expr2 = $3;
            $$->result = $1->result * $3->result;
        }
        | EXPR '/' EXPR
        {
            $$ = new DataOperation(EXP::DIV, DATA::FVAL);
            $$->expr1 = $1;
            $$->expr2 = $3;
            // TODO div by zero
            $$->result = $1->result / $3->result;
        }
        | EXPR '+' EXPR
        {
            $$ = new DataOperation(EXP::PLUS, DATA::FVAL);
            $$->expr1 = $1;
            $$->expr2 = $3;
            $$->result = $1->result + $3->result;
        }
        | EXPR '-' EXPR
        {
            $$ = new DataOperation(EXP::MINUS, DATA::FVAL);
            $$->expr1 = $1;
            $$->expr2 = $3;
            $$->result = $1->result - $3->result;
        }
        | '-' EXPR %prec UMINUS 
        {
            $$ = new DataOperation(EXP::NEG, DATA::FVAL);
            $$->expr1 = $2; 
            $$->result = (-$2->result);
        }
        | SIN_FUNC '[' EXPR ']'
        {
            $$ = new DataOperation(EXP::SIN, DATA::FVAL);
            $$->expr1 = $3; 
            $$->result = qSin($3->result);
        }
        | COS_FUNC '[' EXPR ']'
        {
            $$ = new DataOperation(EXP::COS, DATA::FVAL);
            $$->expr1 = $3; 
            $$->result = qCos($3->result);
        }
        | SQRT_FUNC '[' EXPR ']'
        {
            $$ = new DataOperation(EXP::SQRT, DATA::FVAL);
            $$->expr1 = $3; 
            $$->result = qSqrt($3->result);
        }
        | EXPR POW_FUNC EXPR
        {
            $$ = new DataOperation(EXP::POW, DATA::FVAL);
            $$->expr1 = $1;
            $$->expr2 = $3;
            $$->result = qPow($1->result, $3->result);
        }
        | IVAL 
        {
            $$ = new DataOperation(EXP::NONE, DATA::FVAL);
            $$->result = (float)$1;
        }
        | FVAL 
        {
            $$ = new DataOperation(EXP::NONE, DATA::FVAL);
            $$->result = $1;
        }
        | VARIABLE 
        {
            $$ = new DataOperation(EXP::NONE, DATA::SVAL);
            
            if (GCodeParser::dataVaris.contains($1)){
                $$->vName = $1;
                $$->result = GCodeParser::dataVaris[$1];
            }
            else {
                qInfo() << $1 << "was not defined";
            }
        }
        ;
%%


bool set_data_float(char c, float f) {
    switch (c){
        case 'x':
            gcode_data->coord.setX(f);
            break;
            
        case 'y':
            gcode_data->coord.setY(f);
            break;
            
        case 'z':
            gcode_data->coord.setZ(f);
            break;
            
        case 'i':
            gcode_data->useExtCoord = IJK; // TODO check conflicts
            gcode_data->extCoord.setX(f);
            break;
            
        case 'j':
            gcode_data->useExtCoord = IJK; // TODO check conflicts
            gcode_data->extCoord.setY(f);
            break;
            
        case 'k':
            gcode_data->useExtCoord = IJK; // TODO check conflicts
            gcode_data->extCoord.setZ(f);
            break;
            
        case 'a':
            gcode_data->useExtCoord = ABC; // TODO check conflicts
            gcode_data->extCoord.setX(f);
            break;
            
        case 'b':
            gcode_data->useExtCoord = ABC; // TODO check conflicts
            gcode_data->extCoord.setY(f);
            break;
            
        case 'c':
            gcode_data->useExtCoord = ABC; // TODO check conflicts
            gcode_data->extCoord.setZ(f);
            break;
            
        case 'u':
            gcode_data->useExtCoord = UVW; // TODO check conflicts
            gcode_data->extCoord.setX(f);
            break;
            
        case 'v':
            gcode_data->useExtCoord = UVW; // TODO check conflicts
            gcode_data->extCoord.setY(f);
            break;
            
        case 'w':
            gcode_data->useExtCoord = UVW; // TODO check conflicts
            gcode_data->extCoord.setZ(f);
            break;
            
        case 'r': // radius
        case 's': // pause sec
        case 'q': 
        case 'p': // pause msec
        case 'f': // feed
        case 'd': // diameter
        case 't': {// change tool
            gcode_data->vParams << addParam {c, true, f};
            break;
        }
            
        default:
            gcode_data->decoded = false;
            printf("not decoded char %c, line %d\n", c, gcode_lineno);
            return false;
            break;
    }
    return true;
}

extern int gcode_charno;
  
  
void gcode_error (const char * error) {
  fprintf (stderr, "gcode line %d pos %d: %s\n", gcode_lineno, gcode_charno, error);
//   return 0;
}
